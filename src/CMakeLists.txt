macro(add_eeros_sources)
    file(RELATIVE_PATH _relPath "${PROJECT_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}")
    foreach(_src ${ARGN})
        if(_relPath)
            list(APPEND EEROS_SRCS "${_relPath}/${_src}")
        else()
            list(APPEND EEROS_SRCS "${_src}")
        endif()
    endforeach()
    if(_relPath)
        # propagate EEROS_SRCS to parent directory
        set(EEROS_SRCS ${EEROS_SRCS} PARENT_SCOPE)
    endif()
endmacro()

include_directories(${EEROS_SOURCE_DIR}/includes ${EEROS_BINARY_DIR})

add_subdirectory(core)
add_subdirectory(task)
add_subdirectory(math)
add_subdirectory(control)
add_subdirectory(hal)
add_subdirectory(sequencer)
add_subdirectory(socket)
add_subdirectory(safety)
add_subdirectory(logger)
add_subdirectory(config)
# add_subdirectory(ui)

# EEROS_SRCS only exists in this CMakeLists.txt, so we need to explicitly set it in the main (parent) CmakeLists.txt
set(EEROS_SRCS ${EEROS_SRCS} PARENT_SCOPE)
